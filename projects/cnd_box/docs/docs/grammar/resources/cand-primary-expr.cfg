#---------------------------------------------------------------------------------------------------------------------#
# @license Copyright 2025 Anton Yashchenko
# @project C& Programming Language
# @author(s) Anton Yashchenko
# @website https://www.acpp.dev
#---------------------------------------------------------------------------------------------------------------------#
# @file cand-primary-expr.cfg
# @ingroup cnd_docs
# @brief C& Context Free Grammar for Primary Expressions
#
# The purpose of this CFG is to provide a reference for the C& language parser construction.
# Not all features of the C& language are represented in this grammar due to the limitations of CFG.
# Note that some syntax choices have been made based on the limitations of the Grammophone application.
# Use this applications while designing the grammar to ensure validity.
# https://mdaines.github.io/grammophone/#/
# https://github.com/mdaines/grammophone/wiki
#---------------------------------------------------------------------------------------------------------------------#

$syntax -> $$primary .
$empty -> .

$$primary -> $$primary_expr $SEMICOLON .
$$primary_expr -> $LEFT_PARENTHESIS $$primary_expr $RIGHT_PARENTHESIS | $$list_fold .

$$list_fold -> $$assignment $$list_fold_next .
$$list_fold_next -> $COMMA $$assignment $$list_fold_next | $empty .

$$assignment -> $$logical_or $$assignment_next .
$$assignment_next -> $EQUALS_SIGN $$assignment | $empty .

$$logical_or -> $$logical_and $$logical_or_next .
$$logical_or_next -> $LOGICAL_OR_OPERATOR $$logical_and $$logical_or_next | $empty .

$$logical_and -> $$bitwise_or $$logical_and_next .
$$logical_and_next -> $LOGICAL_AND_OPERATOR $$bitwise_or $$logical_and_next | $empty .

$$bitwise_or -> $$bitwise_xor $$bitwise_or_next .
$$bitwise_or_next -> $VERTICAL_LINE $$bitwise_xor $$bitwise_or_next | $empty .

$$bitwise_xor -> $$bitwise_and $$bitwise_xor_next .
$$bitwise_xor_next -> $CIRCUMFLEX_ACCENT $$bitwise_and $$bitwise_xor_next | $empty .

$$bitwise_and -> $$equality $$bitwise_and_next .
$$bitwise_and_next -> $AMPERSAND $$equality $$bitwise_and_next | $empty .

$$equality -> $$relational $$equality_next .
$$equality_next -> $EQUALITY_OPERATOR $$relational $$equality_next | $NOT_EQUAL_OPERATOR $$relational $$equality_next | $empty .

$$relational -> $$three_way_equality $$relational_next .
$$relational_next -> $GREATER_THAN_SIGN $$three_way_equality $$relational_next | $LESS_THAN_SIGN $$three_way_equality $$relational_next
				  | $GREATER_THAN_EQUAL_SIGN $$three_way_equality $$relational_next | $LESS_THAN_EQUAL_SIGN $$three_way_equality $$relational_next | $empty .

$$three_way_equality -> $$bitwise_shift $$three_way_equality_next .
$$three_way_equality_next -> $SPACESHIP_OPERATOR $$bitwise_shift $$three_way_equality_next | $empty .

$$bitwise_shift -> $$summation $$bitwise_shift_next .
$$bitwise_shift_next -> $BITWISE_RIGHT_SHIFT $$summation $$bitwise_shift_next | $BITWISE_LEFT_SHIFT $$summation $$bitwise_shift_next | $empty .

$$summation -> $$production $$summation_next .
$$summation_next -> $PLUS_SIGN $$production $$summation_next | $HYPHEN_MINUS $$production $$summation_next | $empty .

$$production -> $$prefix $$production_next .
$$production_next -> $ASTERISK $$prefix $$production_next | $SOLIDUS $$prefix $$production_next | $PERCENT_SIGN $$prefix $$production_next | $empty .

$$prefix -> $$prefix_next $$access .
$$prefix_next -> $INCREMENT_SIGN $$prefix | $DECREMENT_SIGN $$prefix | $empty .

$$access -> $$postfix $$access_next .
$$access_next -> $FULL_STOP $$postfix $$access_next | $empty .

$$postfix -> $$resolution $$postfix_next .
$$postfix_next -> $INCREMENT_SIGN $$postfix | $DECREMENT_SIGN $$postfix | $empty .

$$resolution -> $$operand $$resolution_next .
$$resolution_next -> $DOUBLE_COLON $$operand $$resolution_next | $empty .

$$operand -> $$identity_operand | $$value_operand .

$$identity_operand -> $IDENTIFIER .

$$value_operand -> $NUMBER | $REAL | $UNSIGNED_LITERAL | $BIT_LITERAL | $BYTE_LITERAL
			| $STRING_LITERAL | $CHARACTER_LITERAL | $RAW_STRING_LITERAL | $BOOLEAN_LITERAL
			| $NONET_LITERAL .

#---------------------------------------------------------------------------------------------------------------------#
# @license Copyright 2025 Anton Yashchenko
#---------------------------------------------------------------------------------------------------------------------#
