/*******************************************************************/
/* @license Copyright 2025 Anton Yashchenko */
/* @project C& Programming Language */
/* @author(s) Anton Yashchenko */
/* @website https://www.acpp.dev */
/*******************************************************************/
/* @file cand-primary-expr.cfg */
/* @ingroup cnd_docs */
/* @brief C& EBNF Grammar for EBNF playground */
/*******************************************************************/

/*******************************************************************/
/* Note that some syntax choices and ommisions have been made based on the limitations */
/* of the BNF playground application. Specifically: */
/*	- All rule names are surrounded by angle brackets eg. "<rule>" */
/*	- <END_OF_FILE> ::= "\0" is ommitted because the BNF playground does not support end-of-file markers. */
/* Style Guide: */
/*	- Terminal symbols are in all caps snake case. */
/*	- Non-terminal symbols are lowercase snake case. */

/* Precedence Levels from highest to lowest: */
/* - 17 : <list_fold> 		: a,b */
/* - 16 : <assignment> 		: a = b */
/* - 15 : <logical_or> 		: a || b */
/* - 14 : <logical_and> 		: a && b */
/* - 13 : <bitwise_or> 		: a | b */
/* - 12 : <bitwise_xor> 		: a ^ b */
/* - 11 : <bitwise_and> 		: a & b */
/* - 10 : <equality> 			: a == b */
/* 				   			: a != b */
/* - 8 : <relational> 		: a > b */
/*							: a < b */
/*							: a >= b */
/*							: a <= b */
/* - 8 : <three_way_equality> : a <=> b */
/* - 7 : <bitwise_shift> 		: a >> b */
/*					   		: a << b */
/* - 6 : <summation> 			: a + b */
/*                   			: a - b */
/* - 5 : <production> 		: a * b */
/*					 		: a / b */
/*					 		: a % b */
/* - 4 : <prefix> 			: ++a */
/*							: --a */
/*							: !a */
/* - 3 : <access> 			: a.b */
/* - 2 : <postfix> 			: a++ */
/*				 			: a-- */
/* - 1 : <resolution> 		: a::b */
/*******************************************************************/

<LINE_FEED> ::= "\n"
<CARRIAGE_RETURN> ::= "\r"
<HORIZONTAL_TAB> ::= "\t"

<SMALL_LETTER_A> ::= "a"
<SMALL_LETTER_B> ::= "b"
<SMALL_LETTER_C> ::= "c"
<SMALL_LETTER_D> ::= "d"
<SMALL_LETTER_E> ::= "e"
<SMALL_LETTER_F> ::= "f"
<SMALL_LETTER_G> ::= "g"
<SMALL_LETTER_H> ::= "h"
<SMALL_LETTER_I> ::= "i"
<SMALL_LETTER_J> ::= "j"
<SMALL_LETTER_K> ::= "k"
<SMALL_LETTER_L> ::= "l"
<SMALL_LETTER_M> ::= "m"
<SMALL_LETTER_N> ::= "n"
<SMALL_LETTER_O> ::= "o"
<SMALL_LETTER_P> ::= "p"
<SMALL_LETTER_Q> ::= "q"
<SMALL_LETTER_R> ::= "r"
<SMALL_LETTER_S> ::= "s"
<SMALL_LETTER_T> ::= "t"
<SMALL_LETTER_U> ::= "u"
<SMALL_LETTER_V> ::= "v"
<SMALL_LETTER_W> ::= "w"
<SMALL_LETTER_X> ::= "x"
<SMALL_LETTER_Y> ::= "y"
<SMALL_LETTER_Z> ::= "z"

<CAPITAL_LETTER_A> ::= "A"
<CAPITAL_LETTER_B> ::= "B"
<CAPITAL_LETTER_C> ::= "C"
<CAPITAL_LETTER_D> ::= "D"
<CAPITAL_LETTER_E> ::= "E"
<CAPITAL_LETTER_F> ::= "F"
<CAPITAL_LETTER_G> ::= "G"
<CAPITAL_LETTER_H> ::= "H"
<CAPITAL_LETTER_I> ::= "I"
<CAPITAL_LETTER_J> ::= "J"
<CAPITAL_LETTER_K> ::= "K"
<CAPITAL_LETTER_L> ::= "L"
<CAPITAL_LETTER_M> ::= "M"
<CAPITAL_LETTER_N> ::= "N"
<CAPITAL_LETTER_O> ::= "O"
<CAPITAL_LETTER_P> ::= "P"
<CAPITAL_LETTER_Q> ::= "Q"
<CAPITAL_LETTER_R> ::= "R"
<CAPITAL_LETTER_S> ::= "S"
<CAPITAL_LETTER_T> ::= "T"
<CAPITAL_LETTER_U> ::= "U"
<CAPITAL_LETTER_V> ::= "V"
<CAPITAL_LETTER_W> ::= "W"
<CAPITAL_LETTER_X> ::= "X"
<CAPITAL_LETTER_Y> ::= "Y"
<CAPITAL_LETTER_Z> ::= "Z"

<DIGIT_ZERO> ::= "0"
<DIGIT_ONE> ::= "1"
<DIGIT_TWO> ::= "2"
<DIGIT_THREE> ::= "3"
<DIGIT_FOUR> ::= "4"
<DIGIT_FIVE> ::= "5"
<DIGIT_SIX> ::= "6"
<DIGIT_SEVEN> ::= "7"
<DIGIT_EIGHT> ::= "8"
<DIGIT_NINE> ::= "9"

<SPACE> ::= " "
<EXCLAMATION_MARK> ::= "!"
<QUOTATION_MARK> ::= "\""
<NUMBER_SIGN> ::= "#"
<DOLLAR_SIGN> ::= "$"
<PERCENT_SIGN> ::= "%"
<AMPERSAND> ::= "&"
<APOSTROPHE> ::= "'"
<LEFT_PARENTHESIS> ::= "("
<RIGHT_PARENTHESIS> ::= ")"
<ASTERISK> ::= "*"
<PLUS_SIGN> ::= "+"
<COMMA> ::= ","
<HYPHEN_MINUS> ::= "-"
<FULL_STOP> ::= "."
<SOLIDUS> ::= "/"

<COLON> ::= ":"
<SEMICOLON> ::= ";"
<LESS_THAN_SIGN> ::= "<"
<EQUALS_SIGN> ::= "="
<GREATER_THAN_SIGN> ::= ">"
<QUESTION_MARK> ::= "?"
<AT_SIGN> ::= "@"

<LEFT_SQUARE_BRACKET> ::= "["
<REVERSE_SOLIDUS> ::= "\\"
<RIGHT_SQUARE_BRACKET> ::= "]"
<CIRCUMFLEX_ACCENT> ::= "^"
<LOW_LINE> ::= "_"
<BACKTICK> ::= "`"

<LEFT_CURLY_BRACKET> ::= "{"
<VERTICAL_LINE> ::= "|"
<RIGHT_CURLY_BRACKET> ::= "}"
<TILDE> ::= "~"


<PUNCTUATOR> ::= <EXCLAMATION_MARK> | <QUOTATION_MARK> | <NUMBER_SIGN> | <DOLLAR_SIGN> | <PERCENT_SIGN> | <AMPERSAND>
			   | <APOSTROPHE> | <LEFT_PARENTHESIS> | <RIGHT_PARENTHESIS> | <ASTERISK> | <PLUS_SIGN> | <COMMA>
			   | <HYPHEN_MINUS> | <FULL_STOP> | <SOLIDUS> | <COLON> | <SEMICOLON> | <LESS_THAN_SIGN> | <EQUALS_SIGN>
			   | <GREATER_THAN_SIGN> | <QUESTION_MARK> | <AT_SIGN> | <LEFT_SQUARE_BRACKET> | <REVERSE_SOLIDUS>
			   | <RIGHT_SQUARE_BRACKET> | <CIRCUMFLEX_ACCENT> | <LOW_LINE> | <BACKTICK> | <LEFT_CURLY_BRACKET>
			   | <VERTICAL_LINE> | <RIGHT_CURLY_BRACKET> | <TILDE>

<ALPHA> ::= <SMALL_LETTER_A> | <SMALL_LETTER_B> | <SMALL_LETTER_C> | <SMALL_LETTER_D> | <SMALL_LETTER_E>
		  | <SMALL_LETTER_F> | <SMALL_LETTER_G> | <SMALL_LETTER_H> | <SMALL_LETTER_I> | <SMALL_LETTER_J>
		  | <SMALL_LETTER_K> | <SMALL_LETTER_L> | <SMALL_LETTER_M> | <SMALL_LETTER_N> | <SMALL_LETTER_O>
		  | <SMALL_LETTER_P> | <SMALL_LETTER_Q> | <SMALL_LETTER_R> | <SMALL_LETTER_S> | <SMALL_LETTER_T>
		  | <SMALL_LETTER_U> | <SMALL_LETTER_V> | <SMALL_LETTER_W> | <SMALL_LETTER_X> | <SMALL_LETTER_Y>
		  | <SMALL_LETTER_Z> | <CAPITAL_LETTER_A> | <CAPITAL_LETTER_B> | <CAPITAL_LETTER_C> | <CAPITAL_LETTER_D>
		  | <CAPITAL_LETTER_E> | <CAPITAL_LETTER_F> | <CAPITAL_LETTER_G> | <CAPITAL_LETTER_H> | <CAPITAL_LETTER_I>
		  | <CAPITAL_LETTER_J> | <CAPITAL_LETTER_K> | <CAPITAL_LETTER_L> | <CAPITAL_LETTER_M> | <CAPITAL_LETTER_N>
		  | <CAPITAL_LETTER_O> | <CAPITAL_LETTER_P> | <CAPITAL_LETTER_Q> | <CAPITAL_LETTER_R> | <CAPITAL_LETTER_S>
		  | <CAPITAL_LETTER_T> | <CAPITAL_LETTER_U> | <CAPITAL_LETTER_V> | <CAPITAL_LETTER_W> | <CAPITAL_LETTER_X>
		  | <CAPITAL_LETTER_Y> | <CAPITAL_LETTER_Z>

<DIGIT> ::= <DIGIT_ZERO> | <DIGIT_ONE> | <DIGIT_TWO> | <DIGIT_THREE> | <DIGIT_FOUR> | <DIGIT_FIVE> | <DIGIT_SIX>
		  | <DIGIT_SEVEN> | <DIGIT_EIGHT> | <DIGIT_NINE>

<DIGIT_NO_ZERO> ::= <DIGIT_ONE> | <DIGIT_TWO> | <DIGIT_THREE> | <DIGIT_FOUR> | <DIGIT_FIVE> | <DIGIT_SIX>
				  | <DIGIT_SEVEN> | <DIGIT_EIGHT> | <DIGIT_NINE>

<WHITESPACE> ::= <SPACE> | <HORIZONTAL_TAB>
<NEWLINE> ::= <LINE_FEED> | <CARRIAGE_RETURN>
<IGNORED> ::= <WHITESPACE>* | <NEWLINE>*
<PRINTABLE> ::= <ALPHA> | <DIGIT> | <PUNCTUATOR> | <WHITESPACE> | <NEWLINE>
<ALPHA_DIGIT> ::= <ALPHA> | <DIGIT>
<ALPHA_LOW_LINE> ::= <ALPHA> | <LOW_LINE>
<ALPHA_DIGIT_LOW_LINE> ::= <ALPHA_DIGIT> | <LOW_LINE>


<LOGICAL_AND_OPERATOR> ::= <AMPERSAND> <AMPERSAND>
<LOGICAL_OR_OPERATOR> ::= <VERTICAL_LINE> <VERTICAL_LINE>
<EQUALITY_OPERATOR> ::= <EQUALS_SIGN> <EQUALS_SIGN>
<NOT_EQUAL_OPERATOR> ::= <EXCLAMATION_MARK> <EQUALS_SIGN>
<BITWISE_LEFT_SHIFT> ::= <LESS_THAN_SIGN> <LESS_THAN_SIGN>
<BITWISE_RIGHT_SHIFT> ::= <GREATER_THAN_SIGN> <GREATER_THAN_SIGN>
<LESS_THAN_EQUAL_SIGN> ::= <LESS_THAN_SIGN> <EQUALS_SIGN>
<GREATER_THAN_EQUAL_SIGN> ::= <GREATER_THAN_SIGN> <EQUALS_SIGN>
<SPACESHIP_OPERATOR> ::= <LESS_THAN_EQUAL_SIGN> <GREATER_THAN_SIGN>
<COLON_ASSIGN> ::= <COLON> <EQUALS_SIGN>
<PLUS_ASSIGN> ::= <PLUS_SIGN> <EQUALS_SIGN>
<MINUS_ASSIGN> ::= <HYPHEN_MINUS> <EQUALS_SIGN>
<ASTERISK_ASSIGN> ::= <ASTERISK> <EQUALS_SIGN>
<SOLIDUS_ASSIGN> ::= <SOLIDUS> <EQUALS_SIGN>
<PERCENT_ASSIGN> ::= <PERCENT_SIGN> <EQUALS_SIGN>
<AMPERSAND_ASSIGN> ::= <AMPERSAND> <EQUALS_SIGN>
<VERTICAL_LINE_ASSIGN> ::= <VERTICAL_LINE> <EQUALS_SIGN>
<CIRCUMFLEX_ACCENT_ASSIGN> ::= <CIRCUMFLEX_ACCENT> <EQUALS_SIGN>
<BITWISE_LEFT_SHIFT_ASSIGN> ::= <BITWISE_LEFT_SHIFT> <EQUALS_SIGN>
<BITWISE_RIGHT_SHIFT_ASSIGN> ::= <BITWISE_RIGHT_SHIFT> <EQUALS_SIGN>
<INCREMENT_SIGN> ::= <PLUS_SIGN> <PLUS_SIGN>
<DECREMENT_SIGN> ::= <HYPHEN_MINUS> <HYPHEN_MINUS>
<DOUBLE_COLON> ::= <COLON> <COLON>
<ELLIPSIS> ::= <FULL_STOP> <FULL_STOP> <FULL_STOP>
<RIGHT_ARROW> ::= <HYPHEN_MINUS> <GREATER_THAN_SIGN>
<EXTERNAL_PULL_OPERATOR> ::= <VERTICAL_LINE> <HYPHEN_MINUS> <GREATER_THAN_SIGN>


<KEYWORD_PRAGMA> ::= "pragma"
<KEYWORD_PROC> ::= "proc"
<KEYWORD_LIB> ::= "lib"
<KEYWORD_INCLUDE> ::= "include"

<KEYWORD_IMPORT> ::= "import"
<KEYWORD_MAIN> ::= "main"

<KEYWORD_DEF> ::= "def"
<KEYWORD_FN> ::= "fn"
<KEYWORD_USE> ::= "use"
<KEYWORD_NAMESPACE> ::= "namespace"
<KEYWORD_STRUCT> ::= "struct"

<KEYWORD_IF> ::= "if"
<KEYWORD_ELIF> ::= "elif"
<KEYWORD_ELSE> ::= "else"
<KEYWORD_SWITCH> ::= "switch"
<KEYWORD_CASE> ::= "case"
<KEYWORD_DEFAULT> ::= "default"
<KEYWORD_WHILE> ::= "while"
<KEYWORD_FOR> ::= "for"
<KEYWORD_RETURN> ::= "return"
<KEYWORD_BREAK> ::= "break"
<KEYWORD_CONTINUE> ::= "continue"

<KEYWORD_TRUE> ::= "true"
<KEYWORD_FALSE> ::= "false"
<KEYWORD_NONE> ::= "none"

<IDENTIFIER> ::= <ALPHA> <ALPHA_DIGIT_LOW_LINE>*
<NUMBER> ::= <DIGIT>+
<REAL> ::= <NUMBER> <FULL_STOP> <NUMBER>
<UNSIGNED_LITERAL> ::= <NUMBER> <SMALL_LETTER_U>
<BIT_LITERAL> ::= <DIGIT_ZERO> <CAPITAL_LETTER_B> | <DIGIT_ONE> <CAPITAL_LETTER_B>
<BYTE_LITERAL> ::= <NUMBER> <SMALL_LETTER_B> | <NUMBER> <SMALL_LETTER_B>
<BOOL_LITERAL> ::= <KEYWORD_TRUE> | <KEYWORD_FALSE>

<escaped_s_char> ::= <REVERSE_SOLIDUS> (<SMALL_LETTER_N> | <SMALL_LETTER_T> | <SMALL_LETTER_R> | <DIGIT_ZERO> | <REVERSE_SOLIDUS> | <QUOTATION_MARK>)
<s_char_sequence> ::= (<escaped_s_char> | <ALPHA> | <DIGIT> | <WHITESPACE>
		   | <EXCLAMATION_MARK> | <NUMBER_SIGN> | <DOLLAR_SIGN> | <PERCENT_SIGN> | <AMPERSAND> | <APOSTROPHE>
		   | <LEFT_PARENTHESIS> | <RIGHT_PARENTHESIS> | <ASTERISK> | <PLUS_SIGN> | <COMMA> | <HYPHEN_MINUS> | <FULL_STOP>
		   | <COLON> | <SEMICOLON> | <LESS_THAN_SIGN> | <EQUALS_SIGN> | <GREATER_THAN_SIGN> | <QUESTION_MARK> | <AT_SIGN> | <LEFT_SQUARE_BRACKET>
		   | <RIGHT_SQUARE_BRACKET> | <CIRCUMFLEX_ACCENT> | <LOW_LINE> | <BACKTICK> | <LEFT_CURLY_BRACKET> | <VERTICAL_LINE>
		   | <RIGHT_CURLY_BRACKET> | <TILDE>)*
<STRING_LITERAL> ::= <QUOTATION_MARK> <s_char_sequence> <QUOTATION_MARK>

<escaped_c_char> ::= <REVERSE_SOLIDUS> (<SMALL_LETTER_N> | <SMALL_LETTER_T> | <SMALL_LETTER_R> | <DIGIT_ZERO> | <REVERSE_SOLIDUS> | <APOSTROPHE> )
<c_char> ::= (<escaped_c_char> | <ALPHA> | <DIGIT> | <WHITESPACE>
		   | <EXCLAMATION_MARK> | <NUMBER_SIGN> | <DOLLAR_SIGN> | <PERCENT_SIGN> | <AMPERSAND> | <QUOTATION_MARK>
		   | <LEFT_PARENTHESIS> | <RIGHT_PARENTHESIS> | <ASTERISK> | <PLUS_SIGN> | <COMMA> | <HYPHEN_MINUS> | <FULL_STOP>
		   | <COLON> | <SEMICOLON> | <LESS_THAN_SIGN> | <EQUALS_SIGN> | <GREATER_THAN_SIGN> | <QUESTION_MARK> | <AT_SIGN> | <LEFT_SQUARE_BRACKET>
		   | <RIGHT_SQUARE_BRACKET> | <CIRCUMFLEX_ACCENT> | <LOW_LINE> | <BACKTICK> | <LEFT_CURLY_BRACKET> | <VERTICAL_LINE>
		   | <RIGHT_CURLY_BRACKET> | <TILDE>)
<CHAR_LITERAL> ::= <APOSTROPHE> <c_char> <APOSTROPHE>

<r_char_sequence> ::= (<ALPHA> | <DIGIT> | <WHITESPACE> | <PUNCTUATOR> | <NEWLINE>)*
<RAW_STRING_LITERAL> ::= <CAPITAL_LETTER_R> <QUOTATION_MARK> <LEFT_PARENTHESIS> <r_char_sequence> <RIGHT_PARENTHESIS> <QUOTATION_MARK>

<lc_char_sequence> ::= (<ALPHA> | <DIGIT> | <WHITESPACE> | <PUNCTUATOR>)*
<LINE_COMMENT> ::= <BACKTICK> <lc_char_sequence>

<bc_char_sequence> ::= (<ALPHA> | <DIGIT> | <WHITESPACE> | <PUNCTUATOR>)*
<BLOCK_COMMENT> ::= <REVERSE_SOLIDUS> <BACKTICK> <bc_char_sequence> <BACKTICK> <REVERSE_SOLIDUS>


<syntax> ::= <pragmatic_stmt>* | <pragmatic_block>
<pragmatic_block> ::= <LEFT_CURLY_BRACKET> <IGNORED> <pragmatic_stmt_list> <IGNORED> <RIGHT_CURLY_BRACKET>
<pragmatic_stmt_list> ::= <pragmatic_stmt>*
<pragmatic_stmt> ::= <primary_stmt> | <process>


<primary_stmt> ::= <primary_expr> <SEMICOLON>

<primary_expr> ::= <LEFT_PARENTHESIS> <primary_expr> <RIGHT_PARENTHESIS> | <list_fold>

<list_fold> ::= <assignment> (<COMMA> <assignment>)*

<assignment> ::= (<logical_or> <EQUALS_SIGN>)* <logical_or>

<logical_or> ::= <logical_and> (<LOGICAL_OR_OPERATOR> <logical_and>)*

<logical_and> ::= <bitwise_or> (<LOGICAL_AND_OPERATOR> <bitwise_or>)*

<bitwise_or> ::= <bitwise_xor> (<VERTICAL_LINE> <bitwise_xor>)*

<bitwise_xor> ::= <bitwise_and> (<CIRCUMFLEX_ACCENT> <bitwise_and>)*

<bitwise_and> ::= <equality> (<AMPERSAND> <equality>)*

<equality> ::= <relational> (<EQUALITY_OPERATOR> <relational>)*
			  | <relational> (<NOT_EQUAL_OPERATOR> <relational>)*

<relational> ::= <three_way_equality> (<GREATER_THAN_SIGN> <three_way_equality>)*
			  | <three_way_equality> (<LESS_THAN_SIGN> <three_way_equality>)*
			  | <three_way_equality> (<GREATER_THAN_EQUAL_SIGN> <three_way_equality>)*
			  | <three_way_equality> (<LESS_THAN_EQUAL_SIGN> <three_way_equality>)*

<three_way_equality> ::= <bitwise_shift> (<SPACESHIP_OPERATOR> <bitwise_shift>)*

<bitwise_shift> ::= <summation> (<BITWISE_RIGHT_SHIFT> <summation>)*
			  	  | <summation> (<BITWISE_LEFT_SHIFT> <summation>)*

<summation> ::= <production> (<PLUS_SIGN> <production>)*
			  | <production> (<HYPHEN_MINUS> <production>)*

<production> ::= <prefix> (<ASTERISK> <prefix>)*
				| <prefix> (<SOLIDUS> <prefix>)*
				| <prefix> (<PERCENT_SIGN> <prefix>)*

<prefix> ::= <INCREMENT_SIGN>* <access>
			| <DECREMENT_SIGN>* <access>
            | <EXCLAMATION_MARK>* <access>

<access> ::= <postfix> (<FULL_STOP> <postfix>)*

<postfix> ::= <resolution> <INCREMENT_SIGN>*
			 | <resolution> <DECREMENT_SIGN>*

<resolution> ::= <operand> (<DOUBLE_COLON> <operand>)*

<operand> ::= <identity_operand> | <value_operand>

<identity_operand> ::= <IDENTIFIER>

<value_operand> ::= <NUMBER> | <REAL> | <UNSIGNED_LITERAL> | <BIT_LITERAL> | <BYTE_LITERAL> | <BOOL_LITERAL>
			| <STRING_LITERAL> | <CHAR_LITERAL> | <RAW_STRING_LITERAL>

<process> ::= <process_decl> | <process_def>
<process_decl> ::= <KEYWORD_PROC> <IGNORED> <AT_SIGN> <IDENTIFIER> <IGNORED> <SEMICOLON>
<process_def> ::= <KEYWORD_PROC> <IGNORED> <AT_SIGN> <IDENTIFIER> <IGNORED> <COLON> <IGNORED> <process_block>
<process_block> ::= <LEFT_CURLY_BRACKET> <IGNORED> <process_stmt_list> <IGNORED> <RIGHT_CURLY_BRACKET>
<process_stmt_list> ::= <process_stmt>*
<process_stmt> ::= <KEYWORD_PROC> <SEMICOLON>

<library> ::= <library_decl> | <library_def>
<library_decl> ::= <KEYWORD_LIB> <IGNORED> <AT_SIGN> <IDENTIFIER> <IGNORED> <SEMICOLON>
<library_def> ::= <KEYWORD_LIB> <IGNORED> <AT_SIGN> <IDENTIFIER> <IGNORED> <COLON> <IGNORED> <library_block>
<library_block> ::= <LEFT_CURLY_BRACKET> <IGNORED> <library_stmt_list> <IGNORED> <RIGHT_CURLY_BRACKET>
<library_stmt_list> ::= <library_stmt>*
<library_stmt> ::= <KEYWORD_LIB> <SEMICOLON>

<include> ::= <KEYWORD_INCLUDE> <WHITESPACE>+ <STRING_LITERAL> <IGNORED> <SEMICOLON>